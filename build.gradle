plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.0'
    id 'application'
}

group = 'dev.davidCMs.jclicker'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {

    implementation 'com.formdev:flatlaf:3.7'

    compileOnly 'org.jetbrains:annotations:26.0.2'

    implementation 'com.github.hypfvieh:dbus-java-core:5.2.0'
    implementation 'com.github.hypfvieh:dbus-java-transport-junixsocket:5.2.0'

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.17'
    implementation group: 'org.tinylog', name: 'tinylog-impl', version: '2.8.0-M1'
    implementation 'org.tinylog:slf4j-tinylog:2.8.0-M1'

}

application {
    mainClass = 'dev.davidCMs.jclicker.Main'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}


tasks.named("shadowJar") {
    dependencies {
        exclude(dependency("com.kohlschutter.junixsocket:junixsocket-core:2.10.1@pom"))
    }
}

def iconLocation = file("src/main/resources/icon.png")

tasks.register('prepBuildAppDir', Copy) {
    from("AppDir")
    from(iconLocation)
    into(rootProject.layout.buildDirectory.dir("AppDir"))
}

tasks.register("jPackage", Exec) {
    dependsOn shadowJar
    dependsOn prepBuildAppDir

    outputs.upToDateWhen { false }
    outputs.cacheIf { false }
    commandLine(
            "jpackage",
            "--input", "build/libs",
            "--name", project.name,
            "--main-jar", project.name + "-" + version + "-all.jar",
            "--main-class", "dev.davidCMs.jclicker.Main",
            "--type", "app-image",
            "--icon", iconLocation,
            "--dest", rootProject.layout.buildDirectory.dir("AppDir").get().asFile.toString()
    )
    finalizedBy(mergeJPackageToUsr)
}

tasks.register("mergeJPackageToUsr") {
    dependsOn(prepBuildAppDir)
    doLast {
        def original = layout.buildDirectory.dir("AppDir/${project.name}").get().asFile
        def destination = layout.buildDirectory.dir("AppDir/usr").get().asFile

        providers.exec {
            commandLine "chmod", "-R", "u+w", destination.absolutePath
        }

        copy {
            from original
            into destination
        }
        delete(original)
    }
}

tasks.register("buildAppImage-x86_64", Exec) {
    dependsOn(jPackage)
    doFirst {
        def outDir = rootProject.layout.buildDirectory.dir("appimage/").get().asFile
        outDir.mkdirs()
        println("File will be output at $outDir")

        def outFile = file(outDir.toString() + "/$project.name-x86_64.AppImage");
        if (outFile.exists()) outFile.delete()

        commandLine(
                "./appimagetool-x86_64.appimage",
                rootProject.layout.buildDirectory.dir("AppDir").get().asFile.toString(),
                outFile.toString()
        )

    }
}
